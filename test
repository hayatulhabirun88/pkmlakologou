:local send [:parse [/system script get tg_sendMessage source]]
:local tolower [:parse [/system script get func_lowercase source]]
:local param1 [:pick $params 0 [:find $params " "]]
:local param2 [:pick $params ([:find $params " "]+1) [:len $params]]
:local param3 [:pick [:pick $params ([:find $params " "]+1) [:len $params]] ([:find [:pick $params ([:find $params " "]+1) [:len $params]] " "]+1) [:len [:pick $params ([:find $params " "]+1) [:len $params]]]]
:if ([:len [:find $param2 " "]]>0) do={
	:set param2 [:pick [:pick $params ([:find $params " "]+1) [:len $params]] 0 [:find [:pick $params ([:find $params " "]+1) [:len $params]] " "]]
} else={
	:set param3 ""
}

:put $params
:put $param1
:put $param2
:put $param3
:put $chatid
:put $from

:local paramsLower [$tolower $params]
:local param1Lower [$tolower $param1]
:local param2Lower [$tolower $param2]
:local param3Lower [$tolower $param3]

:local sendHelp do={
	:local send [:parse [/system script get tg_sendMessage source]]
	:local txt 
	:set txt ("*Daftar parameter:*%0A%0A")
	:set txt ($txt.">  session count - Menampilkan jumlah user yang sedang aktif %0ACth: _/hotspot session showall_%0A")
	:set txt ($txt.">  session showall - Menampilkan seluruh detail user yang sedang aktif %0ACth: _/hotspot session showall_%0A")
	:set txt ($txt.">  session deauth-by-user _<username>_ - Mencabut sesi perangkat berdasarkan Username. %0ACth: _/hotspot session deauth-by-user admin_%0A")
	:set txt ($txt.">  session deauth-by-ip _<ip>_ - Mencabut sesi perangkat berdasarkan Alamat IP. %0ACth: _/hotspot session deauth-by-ip 192.168.1.1_%0A")
	:set txt ($txt.">  session deauth-by-mac _<mac>_ - Mencabut sesi perangkat berdasarkan Alamat MAC. %0ACth: _/hotspot session deauth-by-mac AB:CD:EF:01:23:45_%0A")
	:set txt ($txt.">  add _<user> <password>_ - Menambah user baru. %0ACth: _/hotspot add admin admin123_%0A")
	:set txt ($txt.">  delete _<user>_ - Menghapus user. %0ACth: _/hotspot delete admin _%0A")
	:set txt ($txt.">  disable _<user>_ - menonaktifkan user. %0ACth: _/hotspot disable admin _%0A")
	:set txt ($txt.">  enable _<user>_ - mengaktifkan user. %0ACth: _/hotspot enable admin _%0A")
	:set txt ($txt.">  change-password _<user> <new-pass>_ - mengganti password user. %0ACth: _/hotspot change-password admin  p4sSw_%0A%0A")
	$send chat=$chatid text=("$txt") mode="Markdown"
}

if ($paramsLower = "help") do={
	$sendHelp
}

:if ($param1Lower="session") do={
	:if ($param2Lower="count") do={
		:local output
		:local hotspot [:len [/ip hotspot active find]]
		:local text "Router ID:* $[/system identity get name] * %0A\
		Hotspot users: _$hotspot online_"
		$send chat=$chatid text=$text mode="Markdown"
	}
	:if ($param2Lower="showall") do={
		:local output
		:foreach activeIndex in=[/ip hotspot active find] do={
			:local activeUser ("*Username*: ".[/ip hotspot active get value-name="user" $activeIndex]."%0A")
			:local activeAddress ("*IP*: ".[/ip hotspot active get value-name="address" $activeIndex]."%0A")
			:local activeMACAddr ("*MAC*: ".[/ip hotspot active get value-name="mac-address" $activeIndex]."%0A")
			:local activeLoginBy ("*Login Method*: ".[/ip hotspot active get value-name="login-by" $activeIndex]."%0A")
			:local activeUptime ("*Uptime*: ".[/ip hotspot active get value-name="uptime" $activeIndex]."%0A")
			:local idleTime ("*Idle Time*: ".[/ip hotspot active get value-name="idle-time" $activeIndex]."%0A")
			:local serverIn ("*Server*: ".[/ip hotspot active get value-name="server" $activeIndex]."%0A")
			:set output ($output.$activeUser.$activeAddress.$activeMACAddr.$activeUptime.$idleTime.$activeLoginBy.$serverIn."%0A")
		}
		$send chat=$chatid text=("$output") mode="Markdown"
	}
	:if ($param2Lower="deauth-by-user") do={
		/ip hotspot active remove [find user="$param3"]
		$send chat=$chatid text=("Sesi User $param3 berhasil dihapus") mode="Markdown"
	}
	:if ($param2Lower="deauth-by-mac") do={
		/ip hotspot active remove [find mac-address="$param3"]
		$send chat=$chatid text=("Sesi MAC $param3 berhasil dihapus") mode="Markdown"
	}
	:if ($param2Lower="deauth-by-ip") do={
		/ip hotspot active remove [find address="$param3"]
		$send chat=$chatid text=("Sesi IP $param3 berhasil dihapus") mode="Markdown"
	}
}
